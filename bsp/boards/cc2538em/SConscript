import os

Import('env')

localEnv = env.Clone()

# scons doesn't let us look to parent directories for source, so the
# bsp/chips/at86rf231/radio.c is off limits from this file. To keep things
# simple, each SConscript file in bsp/chips/* will return a list of objects
# which can be appended to the source list. Don't forget to specify a variant_dir,
# or else the build will occur directly in the chips directory.

rf231 = localEnv.SConscript(
    os.path.join('#','firmware','openos','bsp','chips','at86rf231','SConscript'),
    variant_dir = 'rf231',
    exports     = {'env': env},
)

target   = 'libbsp'
bsp_dir  = os.path.join('#','firmware','openos','bsp','boards','iot-lab_M3')
source   = \
    Glob('*.c') + \
    Glob('source/*.c') + \
    [rf231]

libbsp = localEnv.Library(
    target=target,
    source=source,
)

Alias('libbsp', libbsp)
